/*<pre><b>
/ Program   : sasindex
/ Version   : 1.0
/ Author    : Roland Rashleigh-Berry
/ Date      : 06 January 2003
/ Contact   : roland@rashleigh-berry.fsnet.co.uk
/ Purpose   : To extract the program name and purpose and write it to an index.
/ SubMacros : none
/ Notes     : This extracts the information from the header and assumes all
/             headers follow the style seen in this current macro. IF YOUR
/             HEADERS DIFFER THEN YOU WILL NEED TO CHANGE THIS MACRO. Output is
/             written to "@index.txt".
/ Usage     : %sasindex
/ 
/================================================================================
/ PARAMETERS:
/-------name------- -------------------------description-------------------------
/ 
/================================================================================
/ AMENDMENT HISTORY:
/ init --date-- mod-id ----------------------description-------------------------
/ 
/================================================================================
/ This is public domain software. No guarantee as to suitability or accuracy is
/ given or implied. User uses this code entirely at their own risk.
/===============================================================================*/

%macro sasindex;

filename sasprogs "*.sas";

data _sasindex;
  length name $ 16 purpose $ 80;
  retain name ' ';
  infile sasprogs;
  input;
  if length(_infile_) GT 10 and _infile_=:'/ Program' then do;
    name=left(substr(_infile_,index(_infile_,':')+1));
    purpose=' ';
  end;
  if length(_infile_) GT 10 and _infile_=:'/ Purpose' then do;
    purpose=left(substr(_infile_,index(_infile_,':')+1));
    seq=1;
    output;
    input;
    do while(length(_infile_) GT 10 and _infile_=:'/         ');
      seq=seq+1;
      purpose=left(substr(_infile_,3));
      output;
      input;
    end;
  end;
run;

filename sasprogs clear;

proc sort nodupkey data=_sasindex;
  by name seq;
run;

data _null_;
  file "@index.txt";
  set _sasindex;
  by name;
  if _n_ EQ 1 then do;
    put @1 'Index of all sas programs in this directory with standard headers';
    put @1 '=================================================================';
    put / @1 '(this is a list generated by the %sasindex utility macro)';
  end;
  if first.name then put / @1 name @18 '-' @20 purpose;
  else put @20 purpose;
run;


proc datasets nolist;
  delete _sasindex;
run;
quit;

%mend;
